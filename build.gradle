plugins {
  id 'org.ajoberstar.grgit'

  id "fr.brouillard.oss.gradle.jgitver" version "0.6.1"

  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'

  id 'com.gradle.plugin-publish'
  id 'org.ajoberstar.stutter'
}

apply from: 'gradle/spotless.gradle'
apply from: 'gradle/locking.gradle'
apply from: 'gradle/bintray.gradle'

group = 'care.data4life'

sourceCompatibility = 8

jgitver {
  policy {
    pattern = "feature/(.*)"
    transformations = ['LOWERCASE_EN']
  }

  policy {
    pattern = "(main)"
    transformations = ["IGNORE"]
  }

  nonQualifierBranches = "main"

  useDistance(true)
}

java {
  withSourcesJar()
  withJavadocJar()
}

repositories {
  jcenter()
}

dependencies {
  // gradle
  compileOnly gradleApi()

  // groovy
  compileOnly localGroovy()

  // grgit
  api 'org.ajoberstar.grgit:grgit-core:latest.release'
  compatTestImplementation 'org.ajoberstar.grgit:grgit-core:latest.release'

  // testing
  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

stutter {
  sparse = true
  java(8) {
    compatibleRange '5.0'
  }
  java(15) {
    compatibleRange '6.3'
  }
}

pluginBundle {
  website = 'https://github.com/d4l-data4life/gradle-git-publish'
  vcsUrl = 'https://github.com/d4l-data4life/gradle-git-publish.git'
  description = 'Publishing to Git repositories'
  plugins {
    publishPlugin {
      id = 'care.data4life.git-publish'
      displayName = 'Git Publish Plugin'
      tags = ['publish', 'git']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}

// remove duplicate publication
//gradlePlugin.automatedPublishing = false
